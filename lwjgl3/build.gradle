plugins {
  id 'com.github.johnrengelman.shadow' version '8.1.1'
}

def appName = 'TikTime'
def gdxVersion = '1.11.0'
def projectVersion = '1.0.0'

sourceSets.main.resources.srcDirs += [rootProject.file('assets').path]

mainClassName = 'com.tiktime.lwjgl3.Lwjgl3Launcher'
application {
  mainClass.set(mainClassName)
}

java {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
  implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
  implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
  implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
  implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
  implementation project(':core')
  implementation("com.badlogicgames.gdx:gdx-tools:$gdxVersion") {
    exclude group: 'com.badlogicgames.gdx', module: 'gdx-backend-lwjgl'
  }
}

shadowJar {
  group = 'build'
  description = 'Assemble fat jar including all dependencies'

  from(rootProject.file('assets')) {
    into 'assets'
  }

  archiveClassifier.set('')

  manifest {
    attributes(
      'Main-Class': 'com.tiktime.lwjgl3.Lwjgl3Launcher',
      'Class-Path': 'assets/'
    )
  }
}

jar.enabled = false

run {
  workingDir = rootProject.file('assets').path
  if (System.properties['os.name'].toLowerCase().contains('mac')) {
    jvmArgs += "-XstartOnFirstThread"
  }
}
